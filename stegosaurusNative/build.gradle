apply plugin: 'cpp'

libraries {
  stegosaurus {}
}

executables {
  stegotest {}
}

binaries.all {
  if(toolChain in Gcc) {
    cppCompiler.args "-O2", "-Wall", "-Wextra", "-g", "-pedantic",
                     "-Wsign-compare"
    linker.args "-ljpeg", "-lgtest"

  }
}

task swig(type:Exec) {
  ext.swigPath = "src/main/cpp/stegosaurus.i"
  ext.swigOut = "src/main/cpp/stegosaurus_wrap.cpp"
  /* XXX This is no good */
  ext.swigOutDir = buildDir.getPath() + "/swigClasses/com/stegosaurus/cpp"
  ext.swigPackage = "com.stegosaurus.cpp"
  (new File(swigOutDir)).mkdirs()
  commandLine "swig", "-package", swigPackage, "-c++", "-java",
              "-outdir", swigOutDir, "-o", swigOut, swigPath
}

sources {
  stegosaurus {
    cpp {
      source {
        srcDirs "src/main/cpp", "src/main/c"
        include "**/*.cpp", "**/*.c"
      }
      exportedHeaders {
        srcDirs "src/main/cpp", "src/main/c"
        include "**/*.h"
      }
    }
  }

  stegotest {
    cpp {
      lib libraries.stegosaurus/*.static*/
      source {
        srcDir "src/test/cpp/"
        include "**/*.cpp"
      }
    }
  }
}

/* Not all the tasks we need to add dependencies to exist at first, so
 * we have to wait until the project has been evaluated in its entirety.
 */
afterEvaluate { project ->
  /* Make the installStegotestExecutable task depend on us copying over
   * the resources */
  task resourcesCopy(type:Copy) {
    ext.resourcePath = "src/test/resources/cpp"
    /* XXX again, bad copying */
    ext.targetPath = installStegotestExecutable.destinationDir.getPath() +
                     "/resources"
    file(targetPath).mkdirs()
    from(file(resourcePath))
    into(file(targetPath))
  }
  installStegotestExecutable.dependsOn resourcesCopy

  task test(type:Exec, dependsOn: installStegotestExecutable) {
    workingDir installStegotestExecutable.destinationDir.getPath()
    commandLine "./stegotest"
  }
}

/* SWIG generates some sources, so it's best to have it generated as early
 * as possible, and extractHeaders is the first in the dependency chain of
 * compilation */
stegosaurusCppExtractHeaders.dependsOn swig
